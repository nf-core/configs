// Define the Scratch directory
def scratch_dir = System.getenv("USER_SCRATCH") ?: "/tmp"

params {
    config_profile_name = 'cheaha'
    config_profile_description = 'University of Alabama at Birmingham Cheaha HPC'
    config_profile_contact = 'Lara Ianov (lianov@uab.edu) or Austyn Trull (atrull@uab.edu)'
    config_profile_url = 'https://www.uab.edu/cores/ircp/bds'
}

env {
    TMPDIR="$scratch_dir"
    SINGULARITY_TMPDIR="$scratch_dir"
}

singularity {
    enabled = true
    autoMounts = true
    runOptions = "--contain --workdir $scratch_dir -B /scratch"
}

def getQueues = { time, memory ->
    def queue_list = ["long"]

    if (time <= 2.h) {
        queue_list.add("express")
    }

    if (time <= 12.h) {
        queue_list.add("short")
    }

    if (time <= 50.h) {
        queue_list.add("medium")
    }

    if (memory >= 300.GB) {
        queue_list.add("largemem-long")

        if (time <= 50.h) {
            queue_list.add("largemem")
        }
    }

    return queue_list.join(",")
}

process {
    resourceLimits = [
        memory: 750.GB,
        cpus: 128,
        time: 150.h
    ]
    executor = 'slurm'
    maxRetries = 3
    beforeScript = 'module load Singularity/3.5.2-GCC-5.4.0-2.26'
    queue = { getQueues( task.time, task.memory ) }
}
