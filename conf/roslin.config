//Profile config names for nf-core/configs
params {
    config_profile_description = 'University of Edinburgh (Eddie) cluster profile for Roslin Institute provided by nf-core/configs.'
    config_profile_contact     = 'Sebastien Guizard (@sguizard) and Donald Dunbar (@ddunbar)'
    config_profile_url         = 'https://www.ed.ac.uk/information-services/research-support/research-computing/ecdf/high-performance-computing'
}

executor {
    name = "sge"
}

process {
    stageInMode = 'symlink'
    scratch     = 'false'
    penv        = { task.cpus > 1 ? "sharedmem" : null }

    // This will override all jobs clusterOptions
    // This is necessary to allow jobs to run on Eddie for many users
    // For each job, we add an extra 8 Gb of memory.
    // For example, the process asked 16 Gb of RAM (task.memory). The job will reserve 24 Gb of RAM.
    // The process will still use 16 Gb (task.memory) leaving 8 Gb for other system processes.
    // This is very useful any JAVA programs which allocate task.memory RAM for its Virtual Machine
    // Also it leaves enough memory for singularity to unpack images.
    clusterOptions = {
        def extraMemory  = task.memory.toMega() + 8192 // 8 GB in MB
        def memoryPerCpu = extraMemory / task.cpus
        System.getenv('NFX_SGE_PROJECT') ?
            "-l h_vmem=${memoryPerCpu}M -P ${System.getenv('NFX_SGE_PROJECT')}" :
            "-l h_vmem=${memoryPerCpu}M -P uoe_baseline"
    }

    // common SGE error statuses
    errorStrategy = {task.exitStatus in [143,137,104,134,139,140] ? 'retry' : 'finish'}
    maxErrors = '-1'
    maxRetries = 3

    // Use apptainer module instead of singularity
    beforeScript =
    """
    . /etc/profile.d/modules.sh
    module load igmm/apps/apptainer/1.3.4
    export SINGULARITY_TMPDIR="\$TMPDIR"
    """
}

env {
    MALLOC_ARENA_MAX=1
}

singularity {
    envWhitelist = "SINGULARITY_TMPDIR,TMPDIR"
    runOptions   = '-p -B "$TMPDIR"'
    enabled      = true
    autoMounts   = true
    // Define the singularity cache directory depending on the presence of the NFX_SGE_PROJECT variable
    // User without compute project can't access to the shared cache directory.
    // So, they need to store singularity images into the work directory.
    cacheDir     = System.getenv('NFX_SGE_PROJECT') ?
        "/exports/cmvm/eddie/eb/groups/alaw3_eb_singularity_cache" :
        null
}
