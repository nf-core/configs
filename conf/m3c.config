//Profile config names for nf-core/configs
params {
    config_profile_description = 'The M3 Research Center HPC cluster profile provided by nf-core/configs'
    config_profile_contact     = 'Sabrina Krakau (@skrakau)'
    config_profile_url         = 'https://www.medizin.uni-tuebingen.de/de/das-klinikum/einrichtungen/zentren/m3'
}

singularity {
    enabled = true
}

process {
    resourceLimits = [
        memory: 1843.GB,
        cpus: 128,
        time: 14.d
    ]
    executor         = 'slurm'
    scratch          = 'true'
    containerOptions = '--bind $TMPDIR'
    queue = {
        if (task.label && task.label.toString().contains('gpu')) {
            return 'gpu-a30'
        } else if (task.time > 24.h) {
            return 'cpu3-long'
        } else if (task.memory > 460.GB || task.cpus > 64) {
            return 'cpu2-hm'
        } else {
            return 'cpu1'
        }
    }

    withLabel: 'process_gpu' {
        resourceLimits = [
            memory: 455.GB,
            cpus: 96,
            time: 24.h
        ]
        containerOptions = '--nv --bind $TMPDIR'
        clusterOptions = '--gres=gpu:2'
    }
}

params {
    igenomes_base = '/mnt/lustre/datasets/igenomes'
    max_memory    = 1843.GB
    max_cpus      = 128
    max_time      = 14.d
}

/*
 * By default the nf-co2footprint plugin is not activated. To activate it, please add
 * ````
 * plugins {
 *  id 'nf-co2footprint@<VERSION>'
 * }
 * ```
 * in your config, or use the
 * `-plugins nf-co2footprint@<VERSION>` parameter in your nextflow call.
 */
co2footprint {
    String co2Timestamp = new java.util.Date().format( 'yyyy-MM-dd_HH-mm-ss')

    // Determine timestamp suffix
    String timestampSuffix = params.get('trace_report_suffix') ?: (this.hasProperty('trace_timestamp') ? this.trace_timestamp : co2Timestamp)

    // File parameters
    if (params.containsKey('outdir')) {
        String infoDir = "${params.outdir}/pipeline_info"
        traceFile   = "${infoDir}/co2footprint_trace_${timestampSuffix}.txt"
        reportFile  = "${infoDir}/co2footprint_report_${timestampSuffix}.html"
        summaryFile = "${infoDir}/co2footprint_summary_${timestampSuffix}.txt"
    }

    // Standard parameters
    location = 'DE'
    pue = 1.3
}
