// Profile details
params {
    config_profile_description = "The Wellcome Sanger Institute HPC cluster profile"
    config_profile_contact = 'Priyanka Surana (@priyanka-surana)'
    config_profile_url = 'https://www.sanger.ac.uk'
}

// This option is unscoped
poolSize = 4

// Queue and LSF submission options
process {
    executor = 'lsf'

    // Processes without any explicit labels will fail to run on LSF
    // Set low values as defaults to have a default value
    cpus   = 1
    memory = 6.Gb

    clusterOptions = { task.accelerator ? "-gpu \"num=${task.accelerator.request}/host:mode=shared:j_exclusive=yes\"" : null }

    containerOptions = {
        if (task.accelerator) {
            workflow.containerEngine == "singularity" ? '--containall --cleanenv --nv':
                ( workflow.containerEngine == "docker" ? '--gpus all': null )
        }
    }
}

// Executor details
executor {
    name = 'lsf'
    perJobMemLimit = true
    submitRateLimit = '5 sec'
    killBatchSize = 50
}

// Import cluster-specific settings from ./sanger/
includeConfig ({
    // Default cluster name
    def clustername = "farm22"

    try {
        clustername = ['/bin/bash', '-c', 'lsid | awk \'$0 ~ /^My cluster name is/ {print $5}\''].execute().text.trim()
    } catch (java.io.IOException e) {
        System.err.println("WARNING: Could not run lsid to determine current cluster, defaulting to "+clustername)
    }

    if (clustername == "tol22") {
        return "sanger/tol22.config"
    } else if (clustername == "farm22" || clustername == "casm22") {
        return "sanger/cpu-farms22.config"
    } else if (clustername == "tiger22" || clustername == "cub22") {
        return "sanger/gpu-farms22.config"
    } else {
        return "/dev/null"
    }
}.call())
